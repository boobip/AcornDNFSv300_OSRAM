;*** File Eight *** > DOS08
 TTL - DOS08 -  Compact
 OPT &01
;* Compacts files on disk *

COMPCT JSR READRV
 JSR VSTRNG
 = "Compacting :"
 STA FDRIV
 STA TDRIV
 JSR DIGOUT
 JSR PCRLF
 LDYIM &00  ;Close all files
 JSR VSHUT
 JSR GETLSZ
 JSR GETDIR
 LDY DIRLEN
 STY WORK + &10
 LDAIM &02
 STA WORK + &0E
 LDAIM &00
 STA WORK + &0F
LOOPX LDY WORK + &10
 JSR UNSTEP
 CPYIM &F8
 BNE NOTDUN

 [ &FF=0
 JSR VSTRNG  ;Finished compacting
 = "Compacted "
 ]
 LDA DIRHIG + &07
 SEC
 SBC WORK + &0E
 PHA
 LDA DIRHIG + &06
 ANDIM &03
 SBC WORK + &0F
 JSR DIGOUT
 PLA
 JSR BYTOUT
 JSR VSTRNG
 = " free sectors",CR
 NOP
 RTS

NOTDUN STY WORK + &10
 JSR INFORM
 LDY WORK + &10
 [ &FF=0
 ; This bit used to check for a zero length file
 ; before the bug was fixed! 1.1b -> 1.1c (?)
 LDAAY CATHIG + &06
 ANDIM &30
 ORAAY CATHIG + &05
 ORAAY CATHIG + &04
 BEQ CMPCT4
 ]
 [ &FF=0
 LDAIM &FF
 CLC
 ADCAY CATHIG + &04
 |
 LDAAY CATHIG + &04
 CMPIM &01 ; Does the same as the above
 ]
 LDAIM &00
 STA WORK + &02
 STA WORK + &06
 ADCAY CATHIG + &05
 STA WORK + &0A
 LDAAY CATHIG + &06
 PHP
 JSR ISOLEN
 PLP
 ADCIM &00
 STA WORK + &0B
 LDAAY CATHIG + &07
 STA WORK + &0C
 LDAAY CATHIG + &06
 ANDIM &03
 STA WORK + &0D
 CMP WORK + &0F
 BNE CMPCT2
 LDA WORK + &0C
 CMP WORK + &0E
 BNE CMPCT2
 CLC
 ADC WORK + &0A
 STA WORK + &0E
 LDA WORK + &0F
 ADC WORK + &0B
 STA WORK + &0F
 JMP CMPCT4
CMPCT2 LDA WORK + &0E
 STAAY CATHIG + &07
 LDAAY CATHIG + &06
 ANDIM &FC
 ORA WORK + &0F
 STAAY CATHIG + &06
 LDAIM &00
 STA LINNO
 STA LINNO + &01
 JSR MVDKDA
 JSR DIROUT
CMPCT4 LDY WORK + &10
 JSR PRTINF
 JMP LOOPX

 LNK DOS09
