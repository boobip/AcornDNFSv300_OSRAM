;*** File Four *** > DOS04
 TTL - DOS04 -  Sequential File System
 OPT &01
;FILE 04.  SEQUENTIAL FILES.
;FIND,SHUT,RADR.  CHECKSUMS

;A.C. NORMAN.  DEC 79/JAN 80.


;$ THE AFL CODE GIVEN HERE
;  IS AN INFORMAL GUIDE TO
;  HOW THE ROUTINES ARE CODED,
;  AND MISS OUT SOME FINE BIT-
;  ADJUSTING DETAILS $
;DCL VSHUT(A):
; IF A=0 THEN
;   FOR AA FROM #20 TO #100
;    STEP #20 DO VSHUT(AA) OD
; ELIF SEQFLG&(CBU OR DUF)=0
;   THEN CLEAR BIT IN DCBMAP
; ELSE
;  LOOKUP(FILENAME(A));
;  IF NOTFOUND THEN BRK() FI;
;  IF SEQFLG&DUF THEN
;    UPDATEDIRECTORY();
;    DIROUT() FI;
;  FLUSHIFCBU();
;  CLEAR BIT IN DCBMAP
; FI;

;Handle limits
WHLIM LDXIM &11  ;Get low handle limit
 LDYIM &15  ;High handle limit
SHTAL1 RTS

;Die my FS => close spool & exec
WFDIE JSR SAVITA
 LDAIM BYTECS
 JMP OSBYTE

CLOSE2 JSR WFDIE  ;Kill off exec & spool
SHTALL LDAIM &00
SHTAL0 CLC
 ADCIM &20
 BEQ SHTAL1
 TAY
 JSR VSHUT
 BNE SHTAL0  ;Always

WSHUT LDAIM &20
 STA DUFFLG ; Default value of DUFFLG

 TYA
 BEQ CLOSE2  ;Close all files
 JSR DCRYCH  ;Decrypt handle & check

VSHUT PHA
 JSR CHEEKY  ;Check handle
 BCS VSHUTQ  ;Not open
 LDAAY SEQBIT  ;Close it for me
 EORIM &FF
 AND DCBMAP
 STA DCBMAP
 LDAAY SEQFLG
 ANDIM &60 ; CBU+DUF
 BEQ VSHUTQ  ;Input ] that's all folks!
 JSR VLOOK
 LDAAY SEQFLG
 AND DUFFLG ; $20 for normal close, 0 for just ensure
 BEQ VSHDD
 LDX SEQWB  ;Write back length to dir
 LDAAY SEQLLA
 STAAX CATHIG + &04
 LDAAY SEQLMA
 STAAX CATHIG + &05
 LDAAY SEQLHA
 JSR LFOUR
 EORAX CATHIG + &06
 ANDIM &30
 EORAX CATHIG + &06 ; 2-XOR'S
 STAAX CATHIG + &06
 JSR DIROUT  ;Put dir back on disc
 LDY DCBY
VSHDD JSR BFLUSH  ;Write buffer to disc
VSHUTQ LDX SEQWX
 PLA
 RTS

VLOOK JSR SETQ  ;Set qualif/drive
VLOOK3 LDXIM &06
VSHUTL LDAAY SEQLOK - &02
 STAAX WORK + &0B
 DEY
 DEY
 DEX
 BPL VSHUTL
 JSR LOOKW  ;Y:=DIRECTORY ENTRY
 BCC DSKCHN  ; DISASTER! NOT THERE
 STY SEQWB ; POINTER TO DIRECTORY
 LDY DCBY
CKSAM0 RTS

;Set qualifier and drive
SETQ LDAAY SEQLOK
 ANDIM &7F
 STA QUALIF
 LDAAY SEQFLG
 JMP DODRIV ; may wait if transfer still going

;Check disk same - otherwise throw up
CHKSAM JSR SAVITA
 LDA CYCNO
 JSR GETDIR
 CMP CYCNO
 BEQ CKSAM0
DSKCHN JSR DSKMSG
 = DKCNUM,"changed",&00

;VFIND OPENS A SEQUENTIAL
;FILE. 
;IF C IS SET THE FILE MUST
;ALREADY EXIST, AND IT WILL BE
;SET UP FOR INPUT OR UPDATING.
;IF C IS CLEAR IT WILL BE SET
;UP FOR OUTPUT.
;N.B. IF FIND HAS TO CREATE A
;NEW FILE IT CLOBBERS THE WORK
;VECTOR IN SUCH A WAY THAT ITS
;ARG MUST NOT HAVE BEEN IN THAT
;AREA.


WFIND ANDIM &C0 ; Top 2 bits only
 BNE WFIND0
 JSR SAVITA
 JMP WSHUT
WFIND0 JSR SAVIT
 STX WORK
 STY WORK + &01
 STA ATEMP
 BIT ATEMP
 PHP  ;V flag indicates I/O
 JSR FRMNAM ; COPY NAME TO WORK VEC
 JSR CLRWLD
 JSR LOOKUP ; FIND IN DIRECTORY
 BCS NEWDCB  ;File exists
 PLP  ;File does not exist
 BVC VNEWO  ;OPENOUT ] make a new one
 LDAIM &00  ;OPENIN ] return A=0
 RTS

VNEWO PHP  ;Generate new file
 LDAIM &00  ;Clear FCB
 LDXIM &07
VNEWL STAZX WORK + &02  ;Leave name
 STAAX HIWORK
 DEX
 BPL VNEWL
 LDAIM DFSIZE  ;Default file size(secs)
 STA WORK + &09
 JSR DIRDO  ;Create file

;Find a free DCB to use - Y points to
;file in catalogue
NEWDCB PLP
 PHP
 BVS FIND3
 JSR CHKLOK  ;Check locked if OPENOUT
FIND3 JSR CMPFIL  ;Look for file open
 BCC FIND1
FIND2 LDAAY SEQRDO  ;Check R/O
 BPL FILOPN
 BIT ATEMP  ;Check request R/O
 BMI FILOPN
 JSR NXTFIL  ;Carry on search
 BCS FIND2
FIND1 LDY DCBY  ;Free DCB found ??
 BNE SEQNUL

 JSR ESTRNG
 = TMFNUM,"Too many open",&00
FILOPN JSR ESTRNG
 = FOPNUM,"Open",&00

SEQNUL LDAIM &08  ;Copy from dir to FCB
 STA SEQWC  ;Counter
FILLDA LDAAX CATLOW
 STAAY SEQMAP
 INY
 LDAAX CATHIG
 STAAY SEQMAP
 INY
 INX
 DEC SEQWC
 BNE FILLDA
 LDXIM &10
 LDAIM &00
FILLDB STAAY SEQMAP
 INY
 DEX
 BNE FILLDB ; ZERO TO 2ND HALF.
 LDA DCBY
 TAY
 JSR SFIVE
 ADCIM /(SEQMAP )  ; BUFFER ADDRESS
 STAAY SEQBUF
 LDA DCBBIT
 STAAY SEQBIT
 ORA DCBMAP
 STA DCBMAP
 LDAAY SEQLL
 ADCIM &FF ; carry clear because of preceeding add
 LDAAY SEQLM
 ADCIM &00
 STAAY SEQEM
 LDAAY SEQLH
 ORAIM &0F
 ADCIM &00
 JSR ISOLEN
 STAAY SEQEH  ; EXTENT
 PLP  ;Get R/W status
 BVC VFINDO  ;Was new file
 BMI VFIND1  ;R/W
 LDAIM &80  ;Set R/O bit
 ORAAY SEQRDO
 STAAY SEQRDO
VFIND1 LDAAY SEQLL  ;Move length
 STAAY SEQLLA
 LDAAY SEQLM
 STAAY SEQLMA
 LDAAY SEQLH
 JSR ISOLEN
 STAAY SEQLHA
VFINXX LDA FDRIVE
 ORAAY SEQFLG
 STAAY SEQFLG
 TYA
 JSR SFIVE  ;Encrypt handle
 ORAIM &10
 RTS

VFINDO LDAIM &20
 STAAY SEQFLG
 BNE VFINXX  ;Always

;Find next file open
NXTFIL TXA
 PHA
 JMP CMPFL1

;Look for file open
CMPFIL LDAIM &00  ;No free DCBs found yet
 STA DCBY
 LDAIM &08  ;Fifth bit
 STA YTEMP
 TYA
 TAX
 LDYIM &A0  ;Fifth DCB
CMPFL4 STY ITEMP
 TXA
 PHA
 LDAIM &08
 STA XTEMP
 LDA YTEMP  ;Is it alive anyway ??
 BIT DCBMAP
 BEQ CMPFL5  ;No !!
 LDAAY SEQFLG  ;Check same drive
 EOR FDRIVE
 ANDIM &03
 BNE CMPFL1
CMPFL0 LDAAX CATLOW
 EORAY SEQMAP
 ANDIM &7F  ;Knock off top bit
 BNE CMPFL1
 INX
 INY
 INY
 DEC XTEMP
 BNE CMPFL0
 SEC  ;We have a match !!
 BCS CMPFL2
CMPFL5 STY DCBY  ;Free DCB found - save for later use
 STA DCBBIT 
CMPFL1 SEC  ;Go onto next DCB
 LDA ITEMP
 SBCIM &20
 STA ITEMP
 ASL YTEMP
 CLC
CMPFL2 PLA
 TAX
 LDY ITEMP
 LDA YTEMP
 BCS CMPFL3
 BNE CMPFL4
CMPFL3 RTS

;Ensure file/files depending on Y
ENSUR JSR WOPA
NSRNOW LDA DCBMAP ;********** HMT
 PHA

 LDAIM &00
 STA DUFFLG ; Dont update the cat

 TYA
 BNE ENSUR1
 JSR SHTALL
 BEQ ENSUR0  ;Always
ENSUR1 JSR WSHUT
ENSUR0 PLA
 STA DCBMAP
 RTS

WOPA PHA
 TXA
 PHA
 LDAIM &00
 TSX
 STAAX &109
 PLA
 TAX
 PLA
 RTS

;OSARGS called - only A = 0-2 supported
WARGS JSR SAVITA
 CMPIM &FF
 BEQ ENSUR
 CPYIM &00
 BEQ WARGS1
 CMPIM &03
 BCS WARGSR
 JSR WOPA
 CMPIM &01
 BNE VRADR
 JMP VSTAR
WARGS1 CMPIM &02
 BCS WARGSR
 JSR WOPA
 BEQ RDFSNO

;Return address of rest of command line
 LDAIM &FF
 STAZX &02
 STAZX &03
 LDA LINADR
 STAZX &00
 LDA LINADR + &01
 STAZX &01
WARGSR RTS

;Read filing system number
RDFSNO LDAIM FSYSNO
 TSX
 STAAX &105
 RTS

;Read pointer and length
; A = 0 : Read pointer
; A = 2 : Read length
VRADR JSR DCRYCH
 STY DCBY
 ASLA
 ADC DCBY  ;C=0 FOR VALID A!
 TAY  ; POINTS AT LOW BYTE OF DATA
 LDAAY SEQPL
 STAZX &00
 LDAAY SEQPM
 STAZX &01
 LDAAY SEQPH
 STAZX &02
 LDAIM &00  ;File only 3 bytes long
 STAZX &03
 RTS


;SAVE X,Y. VERIFY Y VALID.
CHEEKY PHA
 STX SEQWX
 TYA
 ANDIM &E0 ; LOW 5 BITS MUST BE 0
 STA DCBY  ; FORCE THEM SO, SAVE Y
 BEQ CHEKYZ ; FAILURE
 JSR SFIVE
 TAY  ; NUMBER 1 TO 7
 LDAIM &00
 SEC
CHEKYA RORA
 DEY
 BNE CHEKYA
;THERE IS ONE BIT FOR EACH
;POSSIBLE HANDLE.
 LDY DCBY
 BIT DCBMAP
 BNE CHEKYB ; OK
CHEKYZ PLA
 SEC ;  CARRY BIT INDICATES FAILURE
 RTS
CHEKYB PLA
 CLC  ; SUCCESS
 RTS

;Decrypt handle in X
DCRYPX PHA
 TXA
 JMP DCRYP2

;Decrypt handle
;Returns carry clear if OK
DCRYPT PHA
 TYA
DCRYP2 CMPIM &10
 BCC DCRYP0
 CMPIM &18
 BCC DCRYP1
DCRYP0 LDAIM &08  ;Will set C and A=0
DCRYP1 JSR LFIVE  ;C = bit 3
 TAY
 PLA
 RTS


;END OF FILE 04.

 LNK DOS05

