 TTL Net initialization and sideways routines. File > LOWL01
 OPT SWRTNS

 JMP LOTRAN
 JMP LOCNMI
 JMP LOQNMI
 JMP LORNMI
;JMP LOWINT

;RECEIVE IRQ
LOWINT
IRQ
 LDAIM FOUR
 BIT VIA +&D ;TEST FOR IRQ FLAGED,IFR REG
 BNE ISVIA
 LDAIM &05 ;REASON CODE IS 5
 RTS ;RETURN IF NOT REQUIRED INT
ISVIA
 TXA
 PHA
 TYA
 PHA
 LDA VIA +&B ;ACR REG
 ANDIM &E3
 ORA STORE
 STA VIA +&B ;RESTORE BITS 2,3,4,ACR REG
 LDA VIA +&A ;CLEAR INT,SR REG
 LDAIM &04
 STA VIA +&D ;CLEAR INT,SR REG
 STA VIA +&E   ;DISABLE IRQ,IER REG

 LDY IRQSTR
 CPYIM ISTOP
 BCS NOMASK
 LDA IMASK
 STA MASKST ;STORE MASK VALUE
 ORAIM &1C ;KNOCK OUT PROTECTION BITS
 STA IMASK
NOMASK
; LDAAY JMPI2H -IRTN ;NIFTY CASE INSTUCTION
 LDAIM JMPI2H
 PHA
 LDAAY JMPI2L -IRTN
 PHA
 RTS

;
; ****** NEVER fall through here ************
;


;CLAIM NMI FIRST TIME ROUND
BOOK
LOCNMI
 BIT INTOFF ;DISABLES NMI
 JSR CPINIT ;INITIALISE ADLC CHIP
 LDAIM &EA ;OSBYTE TUBE TEST
 LDXIM ZERO
 STX ACTIVE ;MAKE MYSELF INACTIVE
 LDYIM &FF
 JSR OSBYTE ;TEST FOR TUBE
 STX TUBEFL
 LDAIM &8F ;OSBYTE NMI CLAIM
 LDXIM &0C
 LDYIM &FF
 JSR OSBYTE ;CLAIM NMI

RCLM ;RECLAIM NMI AND ALL L.L. FUNCTIONS SIDEWAYS CALL
RDCDE
LORNMI
;READS NMI CODE INTO D00 RAM AREA
 LDYIM CDELEN ;LOAD NMI CODE LENGTH
MORECD
 LDAAY NMICDE - ONE
 STAAY RAMCDE - ONE
 DEY
 BNE MORECD

 ;SSTATE SET UP BY JEZZ.STATION LONG TERM STATE E.G. RXCB EXISTS, STOPPED ETC
 LDA ROMSWR ;READ WHICH ROM WE ARE
 STA ROMSTR + ONE ;STORE IT IN D00 CODE
 LDAIM &80
 STA TXCLRl ;FLAG CLEAR TO TRANSMIT
 STA ACTIVE
 LDA STNID
 STA TXSRLO ;STORE MY ADDRESS IN TX SCOUT BUFF AREA
 STY TXSRHI ;ASSUMES Y IS ZERO
 STY TUBLOK ;TUBE LOCK FLAG
 BIT INTON ;ENABLES NMI INTERUPTS BY CLEARING NMI FLIP FLOP
 RTS
; "LOQNMI" MOVED TO LOWL04 !!!!!!!!!

 LNK LOWL02
