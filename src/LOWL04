 TTL Net initialization and sideways routines. File => LOWL04
 OPT SWRTNS

;
; ****** NEVER fall through here ************
;


;INITS ADLC CHIP
CPINIT
 LDAIM &C1  ;+TXRS +RXRS -FRDIS -TDSR -RDSR -TIE -RIE SELCR3
 STA CReg1
 LDAIM &1E  ;NRZ -ABTEX -ABT 8TX 8RX 8RX F
 STA CReg4
 LDAIM ZERO  ;-DTR
 STA CReg3
CPRST
 LDAIM &82  ;+TXRS -RXRS -FRDISC -TDSR -RDSR -TIE +RIE SELCR2
 STA CReg1
 LDAIM &67  ;-RTS +CTXST +CRXST -TXLAST TDRA FLAG 2BYTE +PSE
 STA CReg2
 RTS

;
; ****** NEVER fall through here ************
;

;RELINQUISH NMI AND ALL L.L. NET FUNCTIONS
SAVE
LOQNMI
;SAVE VARS IN FIRST PAGE DYN/STATIC WORKSPACE
 BIT ACTIVE
 BPL NOTACT
WAITCL
 LDA TXJUMP + ONE ;WAIT FOR JUMP VECTOR TO GO TO BASE LEVEL
 CMPIM REC
 BNE WAITCL
 LDA TXJUMP + TWO
 CMPIM :MSB: REC
 BNE WAITCL

 BIT INTOFF ;DISABLES NMI
 BIT INTOFF ;DISABLES NMI
 LDAIM ZERO
 STA TXCLRl ;PREVENT FURTHER TRANSMISSIONS
 STA ACTIVE
 LDYIM FIVE ;NET NMI ID CLAIM NUMBER
NOTACT
 JMP CPRST ;MAKE SURE ALL CURRENT TX ARE ENDED

 LNK LOWL05
 